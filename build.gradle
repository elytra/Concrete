buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
		classpath 'me.tatarka:gradle-retrolambda:3.2.0'
	}
}

plugins {
	id 'net.minecrell.licenser' version '0.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven'

group = "com.elytradev"
archivesBaseName = "Concrete"
version = "0.2.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

retrolambda {
	javaVersion JavaVersion.VERSION_1_6
	defaultMethods true
}

configurations {
	deploy
}

dependencies {
	deploy 'org.apache.maven.wagon:wagon-ssh:2.10'
}

minecraft {
	version = "1.11.2-13.20.0.2226"
	mappings = "snapshot_20170205"

	runDir = "eclipse"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar {
	classifier = 'all'
}

task commonJar(type: Jar) {
	classifier = 'common'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/common/**'
	}
}

task blockJar(type: Jar) {
	classifier = 'block'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/block/**'
	}
}

task networkJar(type: Jar) {
	classifier = 'network'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/network/**'
	}
}

task reflectJar(type: Jar) {
	classifier = 'reflect'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/reflect/**'
	}
}

task inventoryJar(type: Jar) {
	classifier = 'gui'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/inventory/**'
	}
}

task configJar(type: Jar) {
	classifier = 'config'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/config/**'
	}
}

reobf {
	commonJar { mappingType = 'SEARGE' }
	blockJar { mappingType = 'SEARGE' }
	networkJar { mappingType = 'SEARGE' }
	reflectJar { mappingType = 'SEARGE' }
	inventoryJar { mappingType = 'SEARGE' }
	configJar { mappingType = 'SEARGE' }
}


artifacts {
	archives commonJar, blockJar, networkJar, reflectJar, inventoryJar, configJar
}

def parseConfig(File config) {
	config.withReader {
		def prop = new Properties()
		prop.load(it)
		return (new ConfigSlurper().parse(prop))
	}
}

if (file('private.properties').exists()) {
	ext.priv = parseConfig(file('private.properties'))

	uploadArchives {
		repositories.mavenDeployer {
			configuration = configurations.deploy
			pom.artifactId = 'concrete'
			repository(url: "sftp://"+priv.sftpHost+priv.sftpPath) {
				authentication(userName: priv.sftpUser, privateKey: file(priv.sftpKey))
			}
		}
	}
}
