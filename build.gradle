buildscript {
	repositories {
		mavenCentral()
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins {
	id 'net.minecrell.licenser' version '0.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

group = "com.elytradev"
archivesBaseName = "Concrete"
version = "0.3.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
	deploy
}

dependencies {
	deploy 'org.apache.maven.wagon:wagon-ssh:2.10'
}

minecraft {
	version = "1.12.1-14.22.0.2474"
	mappings = "snapshot_20170908"

	runDir = "eclipse"
}

processResources {
	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar {
	classifier = 'all'
}

task commonJar(type: Jar) {
	classifier = 'common'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/common/**'
	}
}

task blockJar(type: Jar) {
	classifier = 'block'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/block/**'
	}
}

task networkJar(type: Jar) {
	classifier = 'network'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/network/**'
	}
}

task reflectJar(type: Jar) {
	classifier = 'reflect'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/reflect/**'
	}
}

task inventoryJar(type: Jar) {
	classifier = 'gui'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/inventory/**'
	}
}

task configJar(type: Jar) {
	classifier = 'config'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/config/**'
	}
}

task resgenJar(type: Jar) {
	classifier = 'resgen'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/resgen/**'
	}
}

task utilpacketsJar(type: Jar) {
	classifier = 'utilpackets'
	from(sourceSets.main.output) {
		include 'com/elytradev/concrete/utilpackets/**'
	}
}

reobf {
	commonJar { mappingType = 'SEARGE' }
	blockJar { mappingType = 'SEARGE' }
	networkJar { mappingType = 'SEARGE' }
	reflectJar { mappingType = 'SEARGE' }
	inventoryJar { mappingType = 'SEARGE' }
	configJar { mappingType = 'SEARGE' }
	resgenJar { mappingType = 'SEARGE' }
	utilpacketsJar { mappingType = 'SEARGE'}
}

artifacts {
	archives commonJar, blockJar, networkJar, reflectJar, inventoryJar, configJar, resgenJar, utilpacketsJar
}

if (file('private.gradle').exists()) {
	apply from: 'private.gradle'
}
